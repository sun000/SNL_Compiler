Program ::= ProgramHead DeclarePart ProgramBody . |
ProgramHead ::= PROGRAM ProgramName |
ProgramName ::= ID |
DeclarePart ::= TypeDecpart VarDecpart ProcDecpart |
TypeDecpart ::= Epsilon | TypeDec |
TypeDec ::= TYPE TypeDecList |
TypeDecList ::= TypeId = TypeDef ; TypeDecMore |
TypeDecMore ::= Epsilon | TypeDecList |
TypeId ::= ID |
TypeDef ::= BaseType | StructureType | ID |
BaseType ::= INTEGER | CHAR |
StructureType ::= ArrayType | RecType |
ArrayType ::= ARRAY [ Low .. Top ] OF BaseType |
Low ::= INTC |
Top ::= INTC |
RecType ::= RECORD FieldDecList END |
FieldDecList ::= BaseType IdList ; FieldDecMore | ArrayType IdList ; FieldDecMore |
FieldDecMore ::= Epsilon | FieldDecList |
IdList ::= ID IdMore |
IdMore ::= Epsilon | , IdList |
VarDecpart ::= Epsilon | VarDec |
VarDec ::= VAR VarDecList |
VarDecList ::= TypeDef VarIdList ; VarDecMore |
VarDecMore ::= Epsilon | VarDecList |
VarIdList ::= ID VarIdMore |
VarIdMore ::= Epsilon | , VarIdList |
ProcDecpart ::= Epsilon | ProcDec |
ProcDec ::= PROCEDURE ProcName ( ParamList ) ; ProcDecPart ProcBody ProcDecMore |
ProcDecMore ::= Epsilon | ProcDec |
ProcName ::= ID |
ParamList ::= Epsilon | ParamDecList |
ParamDecList ::= Param ParamMore |
ParamMore ::= Epsilon | ; ParamDecList |
Param ::= TypeDef FormList | VAR TypeDef FormList |
FormList ::= ID FidMore |
FidMore ::= Epsilon | , FormList |
ProcDecPart ::= DeclarePart |
ProcBody ::= ProgramBody |
ProgramBody ::= BEGIN StmList END |
StmList ::= Stm StmMore |
StmMore ::= Epsilon | ; StmList |
Stm ::= ConditionalStm | LoopStm | InputStm | OutputStm | ReturnStm | ID AssCall |
AssCall ::= AssignmentRest | CallStmRest |
AssignmentRest ::= VariMore := Exp |
ConditionalStm ::= IF RelExp THEN StmList ELSE StmList FI |
LoopStm ::= WHILE RelExp DO StmList ENDWH |
InputStm ::= READ ( Invar ) |
Invar ::= ID |
OutputStm ::= WRITE ( Exp ) |
ReturnStm ::= RETURN |
CallStmRest ::= ( ActParamList ) |
ActParamList ::= Epsilon | Exp ActParamMore |
ActParamMore ::= Epsilon | , ActParamList |
RelExp ::= Exp OtherRelE |
OtherRelE ::= CmpOp Exp |
Exp ::= Term OtherTerm |
OtherTerm ::= Epsilon | AddOp Exp |
Term ::= Factor OtherFactor |
OtherFactor ::= Epsilon | MultOp Term |
Factor ::= ( Exp ) | INTC | Variable |
Variable ::= ID VariMore |
VariMore ::= Epsilon | [ Exp ] | . FieldVar |
FieldVar ::= ID FieldVarMore |
FieldVarMore ::= Epsilon | [ Exp ] |
CmpOp ::= < | = |
AddOp ::= + | - |
MultOp ::= * | / |