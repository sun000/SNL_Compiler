Program        ::= ProgramHead DeclarePart ProgramBody .
ProgramHead    ::= PROGRAM ProgramName
ProgramName    ::= ID
DeclarePart    ::= TypeDecpart VarDecpart ProcDecpart//全部可以空
TypeDecpart    ::= Epsilon
TypeDecpart    ::= TypeDec
TypeDec        ::= TYPE TypeDecList
TypeDecList    ::= TypeId = TypeDef ; TypeDecMore
    TypeDecMore    ::= Epsilon
TypeDecMore    ::= TypeDecList
TypeId         ::= ID
TypeDef        ::= BaseType
TypeDef        ::= StructureType
TypeDef        ::= ID
BaseType       ::= INTEGER
BaseType       ::= CHAR
StructureType  ::= ArrayType
StructureType  ::= RecType
ArrayType      ::= ARRAY [ Low .. Top ] OF BaseType
Low            ::= INTC
Top            ::= INTC
RecType        ::= RECORD FieldDecList END
FieldDecList   ::= BaseType IdList ; FieldDecMore
FieldDecList   ::= ArrayType IdList ; FieldDecMore
    FieldDecMore   ::= Epsilon
FieldDecMore   ::= FieldDecList
IdList         ::= ID IdMore
    IdMore         ::= Epsilon
IdMore         ::= , IdList
    VarDecpart     ::= Epsilon
VarDecpart     ::= VarDec
VarDec         ::= VAR VarDecList
VarDecList     ::= TypeDef VarIdList ; VarDecMore
    VarDecMore     ::= Epsilon
VarDecMore     ::= VarDecList
VarIdList      ::= ID VarIdMore
    VarIdMore      ::= Epsilon
VarIdMore      ::= , VarIdList
    ProcDecpart    ::= Epsilon
ProcDecpart    ::= ProcDec
ProcDec        ::= PROCEDURE ProcName ( ParamList ) ; ProcDecPart ProcBody ProcDecMore
    ProcDecMore    ::= Epsilon
ProcDecMore    ::= PROCDECLARATION
ProcName       ::= ID
    ParamList      ::= Epsilon
ParamList      ::= ParamDecList
ParamDecList   ::= Param ParamMore
    ParamMore      ::= Epsilon
ParamMore      ::= ; ParamDecList
Param          ::= TypeDef FormList
Param          ::= VAR TypeDef FormList
FormList       ::= ID FidMore
    FidMore        ::= Epsilon
FidMore        ::= , FormList
ProcDecPart    ::= DeclarePart
ProcBody       ::= ProgramBody
ProgramBody    ::= BEGIN StmList END
StmList        ::= Stm StmMore
    StmMore        ::= Epsilon
StmMore        ::= ; StmList
Stm            ::= ConditionalStm
Stm            ::= LoopStm
Stm            ::= InputStm
Stm            ::= OutputStm
Stm            ::= ReturnStm
Stm            ::= ID AssCall
AssCall        ::= AssignmentRest
AssCall        ::= CallStmRest
AssignmentRest ::= VariMore := Exp//开头可以空
ConditionalStm ::= IF RelExp THEN StmList ELSE StmList FI
LoopStm        ::= WHILE RelExp DO StmList ENDWH
InputStm       ::= READ ( Invar )
Invar          ::= ID
OutputStm      ::= WRITE ( Exp )
ReturnStm      ::= RETURN
CallStmRest    ::= ( ActParamList )
    ActParamList   ::= Epsilon
ActParamList   ::= Exp ActParamMore
    ActParamMore   ::= Epsilon
ActParamMore   ::= , ActParamList
RelExp         ::= Exp OtherRelE
OtherRelE      ::= CmpOp Exp
Exp            ::= Term OtherTerm
    OtherTerm      ::= Epsilon
OtherTerm      ::= AddOp Exp
Term           ::= Factor OtherFactor
    OtherFactor    ::= Epsilon
OtherFactor    ::= MultOp Term
Factor         ::= ( Exp )
Factor         ::= INTC
Factor         ::= Variable
Variable       ::= ID VariMore
    VariMore       ::= Epsilon
VariMore       ::= [ Exp ]
VariMore       ::= . FieldVar
FieldVar       ::= ID FieldVarMore
    FieldVarMore   ::= Epsilon
FieldVarMore   ::= [ Exp ]
CmpOp          ::= <
CmpOp          ::= =
AddOp          ::= +
AddOp          ::= -
MultOp         ::= *
MultOp         ::= /